import net.fabricmc.loom.task.RemapJarTask

plugins {
	id 'io.github.ladysnake.chenille' version '0.10.0'
	id 'maven-publish'
}

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

sourceSets {
	dummy
}

chenille {
	configureTestmod {
		withBaseTestRuns()
	}

	license = 'LGPL'
	javaVersion = 17
}

repositories {
	mavenCentral()
	mavenLocal()
	chenille.repositories {
		cursemaven()
		ladysnake()
		lucko()
		jitpack()
		terraformers()
		shedaniel()
	}
	maven {
		name = 'Quilt Snapshots'
		url = 'https://maven.quiltmc.org/repository/snapshot'
	}
	maven {
		url = "https://maven.siphalor.de/"
		content {
			includeGroup 'de.siphalor'
		}
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "org.quiltmc:quilt-mappings:${minecraft_version}+build.${quilt_mappings}:intermediary-v2"
	modImplementation "org.quiltmc:quilt-loader:${project.loader_version}"

	modImplementation "org.quiltmc:qsl:${qsl_version}+${minecraft_version}"
	modImplementation "org.quiltmc.quilted-fabric-api:quilted-fabric-api:${quilted_fabric_api_version}-${minecraft_version}"

	modIncludeImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${cca_version}"
	modIncludeImplementation ("dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${cca_version}")
	modIncludeImplementation "me.lucko:fabric-permissions-api:${fpa_version}"

	compileOnly "com.demonwav.mcdev:annotations:1.0"

	modCompileOnly "curse.maven:backslot-402739:${backslot_version}"	// not updated yet
	modLocalRuntime ("me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}") { transitive = false }

	modLocalRuntime "com.terraformersmc:modmenu:${modmenu_version}"
	modCompileOnly modLocalRuntime("io.github.ladysnake:elmendorf:${elmendorf_version}")

	testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
	testImplementation project(":junit-loader")

	annotationProcessor(sourceSets.dummy.output)
}

test {
	useJUnitPlatform()

	maxHeapSize = '1G'
}

processResources {
	inputs.property "version", project.version

	filesMatching("quilt.mod.json") {
		expand "version": project.version
	}
}

java {
	withSourcesJar()
}

jar {
	from("COPYING.LESSER") {
		rename { "LICENSE_${project.archivesBaseName}"}
	}
}

remapJar {
	addNestedDependencies.set(false)
}

tasks.register("standaloneJar", Jar) {
	from sourceSets.main.output
	archiveClassifier.set('standalone-dev')
}

tasks.register("remapStandaloneJar", RemapJarTask) {
	dependsOn(project.standaloneJar)
	archiveClassifier.set('standalone')
	inputFile.set(standaloneJar.archiveFile.get())
	addNestedDependencies.set(true)
}

tasks.named("assemble") {
	dependsOn(remapStandaloneJar)
}

chenille {
	configurePublishing {
		mainArtifact = remapStandaloneJar.archiveFile
		withArtifactory()
		withCurseforgeRelease()
		withGithubRelease()
	}
}